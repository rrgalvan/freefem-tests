verbosity=0;

//,---------------------------------------------------------------------
// Circle mesh
//`---------------------------------------------------------------------
real R=1.5;
border Gamma(t=0, 2*pi) { x=R*cos(t);  y=R*sin(t); };
int nx = 900;
mesh Th = buildmesh(Gamma(nx));
//,---------------------------------------------------------------------
// Square mesh
//`---------------------------------------------------------------------
// int nx = 30;
// mesh Th = square(nx, nx, [4*x-2, 4*y-2]);

// Unknown and test functions
fespace Uh(Th, P1);
Uh u, ub, u0;
Uh u0s, u0r; // u0^s, u0^r

fespace Vh(Th, P1);
Vh v, vb, v0;
fespace Wh(Th, P1);
Vh w, wb, w0;

//,---------------------------------------------------------------------
//| Time parameters
//`---------------------------------------------------------------------
real t = 0;
int niter = 250;
real dt = 1.e-2;

real tau=0; // tau=0 => elliptic repulsion, tau=1 => parabolic repulsion
bool forcePositivity=true;

//,---------------------------------------------------------------------
//| Keller-Segel data
//`---------------------------------------------------------------------
// real k0=1, k1=0.2, k2=1, k3=0.1, k4=1;
real ChiAttraction=1,    XiRepulsion=1;
real alpha=1, beta=1, gamma=1, delta=1;
real r=1, s=1;

// real alpha=30, x0=0.3, y0=0.3;
real C0=100, C1=30, x0=0.0, y0=0.0;
real infU = 2;
func initialUGaussian = infU + C0*exp( -C1*( (x-x0)^2 + (y-y0)^2 ) );
func initialURiemann = x<0;
u0 = initialUGaussian;
u0s = pow(u0, s);
v0 = u0;
w0 = u0;

// func initialU = 1.15*exp(-x^2-y^2)*(4-x^2)^2*(4-y^2)^2;
// func initialV = 0.55*exp(-x^2-y^2)*(4-x^2)^2*(4-y^2)^2;
// u0 = initialU;
// v0 = initialV;

real integralU0=int2d(Th)(u0);
cout <<"La integral del dato inicial es "<< integralU0 << "(4*pi=" << 4*pi << ")" << endl;

string testName = "KS_" +
  "dt:" + dt + "_nx:" + nx + "_ChiAttraction=" + ChiAttraction + "_XiRepulsion=" + XiRepulsion;
  // "_k0" + k0 + "_k1:" + k1 + "_k2:" + k2 + "_k3:" + k3 + "_k4:" + k4;
cout << "Running test " << testName << endl;

//,---------------------------------------------------------------------
//| Variational problems
//`---------------------------------------------------------------------

// Macros..............................................................>
macro grad(u) [ dx(u), dy(u) ] // End of Macro
  ; // for emacs :)
macro div(u,v) ( dx(u) + dy(v) ) // End of Macro
  ; // for emacs :)

// (Coninuous FEM) Keller Segel problem
problem KSAttRepU(u,ub)=
  int2d(Th)( u*ub/dt + grad(u)'*grad(ub)
	     - ChiAttraction*u*grad(v)'*grad(ub) // Attractive chemotaxis
	     + XiRepulsion*u*grad(w)'*grad(ub) // Repulsive chemotaxis
	     )
  + int2d(Th)( u0s*ub - u0*ub/dt );

problem KSAttRepV(v,vb)=
  int2d(Th)( v*vb/dt + grad(v)'*grad(vb) + beta*v*vb )
  + int2d(Th)( alpha*u0s*vb )
  + int2d(Th)( - v0*vb/dt );

problem KSAttRepW(w,wb)=
  int2d(Th)( tau*w*wb/dt + grad(w)'*grad(wb) + delta*w*wb )
  + int2d(Th)( - gamma*u0*wb )
  + int2d(Th)( - tau*w0*wb/dt );

for (int k=1; k<=niter; k++) {
  t=t+dt;
  cout << "iter = " << k << "\t (t = " << t << ")" << endl;

  KSAttRepV; // Compute v
  if(forcePositivity) { v = max(0, v); }

  KSAttRepW; // Compute w
  if(forcePositivity) { w = max(0, w); }

  KSAttRepU; // Compute u from u,v
  if(forcePositivity) { u = max(0, u); }

  u0s = pow(u0, s);
  // solve Problem(u, ub)=
  //   int2d(Th)(u*ub)
  //   - int2d(Th)(1*ub);

  u0 = u;  // Prepare next iteration
  // u0s = pow(u0, 0.2);
  v0 = v;
  if(tau != 0) { w0 = w; }

  bool printResults = true; if(printResults) {
    real uMax = u[].max; real uMin=u[].min;
    real vMax = v[].max; real vMin=v[].min;
    real wMax = w[].max; real wMin=w[].min;
    cout << "  max(u) = " << uMax << "  min(u) = " << uMin
	 << (uMin<0 ? " ...---... u: Positivity broken!!!" : "") << endl;
    if(uMax>1.e+10) {
      cout << "BLOW UP FOUND!!" << endl;
      break;
    }
    cout << "  max(v) = " << vMax << "  min(v) = " << vMin
	 << (vMin<0 ? " ...---... v: Positivity broken!!!" : "") << endl;
    cout << "  max(w) = " << wMax << "  min(w) = " << wMin
	 << (wMin<0 ? " ...---... w: Positivity broken!!!" : "") << endl;
    real uInt = int2d(Th)(u); real vInt = int2d(Th)(v); real wInt = int2d(Th)(w);
    cout << "  int_Th(u) = " << uInt << "  int_Th(v) = " << vInt
	 << "  int_Th(w) = " << wInt << endl;
    cout << "  inf(v0)*exp(-beta *t) = " << infU*exp(-beta*t) << endl;
    cout << "  delta*int(w) - gamma*int(u^r) ***trampa, solo r=1*** = " << delta*wInt - gamma*uInt << endl;
  }

  bool plotResults = true; if(plotResults) {
    plot(u, cmm="U", value=1, fill=1, dim=3);
    plot(v, cmm="V", value=1, fill=1, dim=3);
    plot(w, cmm="W", value=1, fill=1, dim=3);
  }

  bool saveToVTK=false; if(saveToVTK) {
    load "iovtk";
    string vtkFile="/tmp/" + testName + ".vtk";
    int[int] vtkOrder=[0];
    savevtk(vtkFile, Th, u, v, w, dataname="u v w", order=vtkOrder);
  }

 }
