// Problem (P):
// u_t - \mu \Delta u + b \cdot \grad u = 0   en \Omega,
// u = 0   sobre \partial\Omega x (0,t_f)
// u(\cdot,0)=u0 sobre \Omega
//
// We take:
//   * \Omega= unit circle,
//   * b(x1,x2) = [x2, -x1]
//   *  A=10, x0=0.3, y0=0.3 (e.g.)
//   * Initial condition u0 = exp( -A*( (x-x0)^2 + (y-y0)^2 ) )
verbosity=0;
load "iovtk";


//,---------------------------------------------------------------------
//| Mesh and finite elements
//`---------------------------------------------------------------------
border Gamma(t=0, 2*pi) { x=cos(t);  y=sin(t); };
int nx = 100;
mesh Th = buildmesh(Gamma(nx));

fespace Uh(Th, P1dc);
Uh u, ub; // Unknown and test function
Uh u0;
Uh b1 = y, b2 = -x;
macro b() [b1, b2] // EOM
  ;

//,---------------------------------------------------------------------
//| Time parameters
//`---------------------------------------------------------------------
real t = 0;
int niter = 200;
real dt = 5.e-2;

//,---------------------------------------------------------------------
//| Problem data
//`---------------------------------------------------------------------

real diffusionCoeff = 1.e-3;
real penaSIP = 4; // SIP DG interior penalty parameter
real eta = 1; // UPW parameter

real alpha=10, x0=0.3, y0=0.3;
real mu=2*alpha;
func initialUGaussian = exp( -alpha*( (x-x0)^2 + (y-y0)^2 ) );
func initialURiemann = x<0;
u0 = initialUGaussian;

real integralU0=int2d(Th)(u0);
cout <<"La integral del dato inicial es "<< integralU0 << endl;


//,---------------------------------------------------------------------
//| Variational problems
//`---------------------------------------------------------------------

// Macros..............................................................>
macro grad(u) [ dx(u), dy(u) ] // End of Macro
  ; // for emacs :)
macro div(u,v) ( dx(u) + dy(v) ) // End of Macro
  ; // for emacs :)
macro dn(u) ( N.x*dx(u) + N.y*dy(u) ) //  Normal derivative
  ; // for emacs :)
macro dotN(v) ( N.x*v[0] + N.y*v[1]) // (v1,v2) * (Normal Vector)
  ; // for emacs
macro intInteriorEdges(Th, expr) ( intalledges(Th)( real(nTonEdge-1)/nTonEdge * (expr) ) ) // EOM
  ; // for emacs
macro intAllEdges(Th, expr) ( intalledges(Th)( (expr) * 1/nTonEdge) ) // EOM
  ; // for emacs

// Diffusion term (without Dirichlet b.c.) using SIP DG
NewMacro aSIP(coeff, u, ub)
  (
   int2d(Th)( coeff * grad(u)'*grad(ub) )  // Diffusion
   - intInteriorEdges(Th, (coeff) * (
				   mean(dn(u))*(-jump(ub))  +
				   (-jump(u))*mean(dn(ub))
				    ) )
   + intInteriorEdges(Th, (coeff) * (
		      penaSIP/lenEdge * jump(u)*jump(ub)
				) )
   ) EndMacro;

// Upwind convection
NewMacro aUPW(coeff, b, u, ub)
  (
   int2d(Th)( (coeff) * b'*grad(u) * ub )
   + intInteriorEdges(Th,
		      (coeff) * (
			- dotN(b) * (-jump(u)) * mean(ub)
			+ eta/2 *abs(dotN(b)) * jump(u)*jump(ub)
			) )
   ) EndMacro;
;


// Godunov flux convection
macro GodunovFlux(u, v, n) (v'*n*mean(u) - 0.5*abs(v'*n)*(-jump(u))) // EOM
  ;

NewMacro aGodunov(coeff, b, u, ub)
  (
   -int2d(Th)( (coeff) * u * (b'*grad(ub)) )
   + intInteriorEdges(Th,
		      (coeff) * (
			(dotN(b))*mean(u)*(-jump(ub))
			- 0.5*abs(dotN(b))*jump(u)*jump(ub)
			) )
   ) EndMacro;
;

// ....................................................................>

problem ConvectDiffImplicitEulerDG(u,ub) =
  int2d(Th) (u*ub/dt)
  + aUPW(1, b , u, ub)
  + aSIP(diffusionCoeff, u, ub)
  - int2d(Th)(u0*ub/dt);

real theta; // if theta=0.5: Crank-Nicholson
problem ConvectDiffThetaSchemeDG(u,ub) =
  int2d(Th) (u*ub/dt)
  + aUPW( theta, b, u, ub )
  + aUPW( (1-theta), b, u0, ub )
  + aSIP( theta*diffusionCoeff, u, ub )
  + aSIP( (1-theta)*diffusionCoeff, u0, ub )
  - int2d(Th)(u0*ub/dt);

// Bilinear form for variational formulation of convection
varf varfConvectThetaSchemeDG(u,ub) =
  int2d(Th) (u*ub/dt)
  + aUPW( theta, b, u, ub );

// Linear form for variational formulation of convection
// varf varfConvectThetaSchemeDG(unused,ub) =
//   int2d(Th)(u0*ub/dt)
//   + aUPW( -(1-theta), b, u0, ub ); // - aUPW in RHS

// Bilinear form for mass matrix
varf varfMassLump(u,ub) =
  int2d(Th, qft=qf1pTlump) (u*ub/dt);

// Bilinear form for convection matrix
varf varfConvectUPW(u, ub) =
  aUPW( 1, b, u, ub );

// Bilinear form for convection matrix
varf varfConvectGodunov(u, ub) =
  aGodunov( 1, b, u, ub );

varf varfDiffusionSIP(u, ub) =
  aSIP(1, u, ub);

// ---------------------------------------------------------------------
// Upwinding of the Transport matrix, K.
//
// Macro stores the result in the matrix Kupw.
//
// See "High-Order Local Maximum Principle Preserving (MPP)
// Discontinuous Galerkin Finite Element Method for the Transport
// Equation". R. Anderson, V. Dobrev, Tz. Kolev, D. Kuzmin, M. Quezada
// de Luna, R. Rieben and V. Tomov
// ---------------------------------------------------------------------
NewMacro upwindTransport(K, Kupw)
{
  int[int] I(1),J(1); real[int] C(1);
  [I,J,C]=K; // get the sparse terms of the matrix K (the arrays are resized)
  int n=K.n;
  int k=0;
  for(int i=0; i<n; i++) {
    Kupw(i,i) = K(i,i);
    // cout << "K_ii=" << Kupw(i,i) << endl;
    while(I[k]==i) {
      // cout << "k=" << k << endl;
      int j = J[k];
      if(i!=j) {
	real tmp = max(0.0, max(-K(i,j), -K(j,i)));  // max(0,-K(i,j), -K(j,i))1
	Kupw(i,j) = K(i,j) + tmp;
	Kupw(i,i) = Kupw(i,i) - tmp;
      }
      k++;
      if(k==I.n) break;
    }
  }
} EndMacro;
;

//,---------------------------------------------------------------------
//| Time loop
//`---------------------------------------------------------------------

// 1) Define matrix
matrix M = varfMassLump(Uh, Uh);   // Mass matrix
matrix K = varfConvectGodunov(Uh, Uh); // Convection matrix
matrix S = varfDiffusionSIP(Uh,Uh); // Diffusion matrix
// K = K + diffusionCoeff*S;

matrix Kupw = K; // Upwinded convection matrix
upwindTransport(K, Kupw); // Perform upwinding and store result in Kupw

matrix Supw = S;
upwindTransport(S, Supw); // Perform upwinding and store result in Kupw
matrix A;
A = M + (-1)*Supw + (-1)*K;

set(A, solver=UMFPACK);

for (int k=1; k<=niter; k++){
  t=t+dt;
  cout << "iter = " << k << "\t (t = " << t << ")" << endl;

  // Solve system ······················································

  // 2) Define RHS
  real[int] F = M*u0[];
  // F += Kupw*u0[];

  // 3) Solve system
  u[] = A^-1*F;

  // ···································································

  real uMax = u[].max; real uMin=u[].min;
  cout << "  max(u) = " << uMax << "  min(u) = " << uMin
       << (uMin<0 ? " ...---... Positivity broken!!!" : "") << endl;
  cout << "  int_Th(u) = " << int2d(Th)(u) << endl;

  u0 = u;  // Prepare next iteration

  plot(u, value=1, fill=1, dim=3);

  bool plotToVTK=false; if(plotToVTK) {
    string vtkFile="/tmp/conv_diff_" + k + ".vtk";
    int[int] vtkOrder=[0];
    savevtk(vtkFile, Th, u, dataname="u", order=vtkOrder);
  }


 }
